

# dApp开发快速入门

dApp，Decentralized Application的缩写，即去中心化应用。在IC上一个完整dApp的开发，需要涵盖以下几个方面内容:
- 合约开发
- 合约测试
- UI设计与前端页面开发
- 前后端功能对接

一般需要有产品经理、合约工程师、测试工程师、前端工程师、美工设计等不同角色共同参与开发。

## 合约开发
在IC，合约是由Motoko语言或Rust语言编写，编译为WASM后部署在Canister中运行。
合约开发主要涉及Motoko语言、IC的SDK(软件开发工具包)、Rust语言等
### Motoko语言
Motoko 编程语言是一种新型、现代且类型安全的语言，适用于想在IC上构建下一代dApp的开发人员。Motoko 专门设计用于支持IC的独特功能并提供熟悉而强大的编程环境。作为一种新语言，Motoko 不断发展，支持新功能和其他改进。

Motoko 编译器、文档和其他工具都是[开源的](https://github.com/dfinity/motoko)。

Motoko语言其主要有以下特点：
#### _原生地支持Canister智能合约_
Motoko 原生地支持Canister智能合约

一个Canister 智能合约（或简称 Canister）被表示为一个 Motoko actor。Actor 是一个自治对象，完全封装其状态并仅通过异步消息与其他 Actor 进行通信。

例如，此代码定义了一个有状态的`Counter` actor
```js
actor Counter { // 定义一个命名为Counter的actor

  var value = 0;

  public func inc() : async Nat { // 定义一个公开的函数
    value += 1; // 全局变量value的值加1
    return value; 
  };
}
```
它的单个公共函数`inc()`可以由该actor和其他actor调用，以更新和读取其私有字段`value`的当前状态

#### _以直接方式顺序编码_
在IC上，Canister可以通过发送异步消息与其他Canister进行通信。

异步编程很困难，但 Motoko 使你能够以更简单、顺序的方式编写异步代码。异步消息是返回 `future` 的函数调用，该`await`构造允许程序暂停执行，直到 `future` 完成。这个简单的功能避免了其他语言中显式异步编程的“回调地狱”问题
```js
actor Factorial {

  var last = 1;

  public func next() : async Nat {
    last *= await Counter.inc(); // await会暂停程序执行，等待Counter合约的inc函数完成且返回值
    return last;
  }
};

ignore await Factorial.next(); // 结果为1 * 1 = 1
ignore await Factorial.next(); // 结果为1 * 2 = 2
await Factorial.next(); // 结果为 2 * 3 = 6
```
#### _现代类型系统_
Motoko 的语法设计对于熟悉 JavaScript 和其他流行语言的人来说是直观的，而且motoko提供了相应现代功能，例如健全的结构类型、泛型、变体类型和静态检查模式匹配。
```js
type Tree<T> = {
  #leaf : T;
  #branch : {left : Tree<T>; right : Tree<T>};
};

func iterTree<T>(tree : Tree<T>, f : T -> ()) {
  switch (tree) {
    case (#leaf(x)) { f(x) };
    case (#branch{left; right}) {
      iterTree(left, f);
      iterTree(right, f);
    };
  }
};

// 累加树的叶子节点的值
let tree = #branch { left = #leaf 1; right = #leaf 2 };
var sum = 0;
iterTree<Nat>(tree, func (leaf) { sum += leaf });
sum
```
#### 自动生成IDL文件
Motoko actor 始终向其客户端呈现一个类型化接口，作为一组具有参数和（未来）结果类型的命名函数

Motoko 编译器（和 SDK）可以以一种称为 Candid 的中性语言格式声明接口，因此支持 Candid 的其他Canister、浏览器驻留代码和智能手机应用程序都可以使用该actor的服务。Motoko 编译器可以使用和生成 Candid 文件，从而允许 Motoko 与其他编程语言实现的Canister无缝交互（前提是它们支持 Candid

例如，之前的 Motoko `Counter` actor 有以下 Candid 接口：
```candid
service Counter : {
  inc : () -> (nat);
}
```
#### 正交持久性
IC在执行时会保留Canister的内存和其他状态。因此，Motoko actor 的状态，包括其内存中的数据结构，可以无限期地保存。Actor 状态不需要针对每条消息显式“重新存储”和“保存”到外部存储。

例如，在以下为`Registry`给名称分配顺序 ID 的actor（容器）中，即使actor的状态在许多IC的节点机器上复制，并且通常不驻留，哈希表的状态也会在调用之间保留。
```js
import Text "mo:base/Text";
import Map "mo:base/HashMap";

actor Registry {

  let map = Map.HashMap<Text, Nat>(10, Text.equal, Text.hash); // 声明一个HashMap，其状态会保留在Canister中

  public func register(name : Text) : async () {
    switch (map.get(name)) {
      case null {
        map.put(name, map.size());
      };
      case (?id) { };
    }
  };

  public func lookup(name : Text) : async ?Nat {
    map.get(name);
  };
};

await Registry.register("hello");
(await Registry.lookup("hello"), await Registry.lookup("world"))
```
#### 可升级
Motoko 提供了许多功能来帮助您利用正交持久性，包括允许您在升级容器代码时保留容器数据。

例如，Motoko 允许您将某些变量声明为`stable`. `stable`的变量值会在容器升级过程中自动保留。

例如s一个stable的计数器：
```js
actor Counter {

  stable var value = 0;

  public func inc() : async Nat {
    value += 1;
    return value;
  };
}

```
它可以安装到Canister中、递增n次，然后不间断地升级到更丰富的实现，例如：
```js
actor Counter {

  stable var value = 0;

  public func inc() : async Nat {
    value += 1;
    return value;
  };

  public func reset() : async () {
    value := 0;
  }
}
```

由于`value`已声明`stable`，因此服务的当前状态n会在升级后保留。计数将从n开始继续，而不是从0重新开始。

由于新接口与前一个接口兼容，引用该容器的现有客户端将继续工作，但新客户端将能够利用其升级的功能（附加reset功能）。

对于无法单独使用稳定变量解决的场景，Motoko 提供了用户可定义的升级hooks，这些hooks在升级之前和之后立即运行，并允许您将任意状态迁移到稳定变量。
#### 还有更多
Motoko 还提供了很多开发人员生产力功能，包括子类型、任意精度算术和垃圾回收。

Motoko 不是、也无意成为实现Canister智能合约的唯一语言。如果它不能满足您的需求，还有一个适用于 Rust 编程语言的容器开发套件 (CDK)。IC的目标是使任何语言（具有对应的 WebAssembly 编译器）都能够生成在IC上运行的Canister智能合约，并通过语言中立的 Candid 接口与其他（可能是其他语言实现的）Canister智能合约进行互操作。

其量身定制的设计意味着 Motoko 是互联网计算机上最简单、最安全的编码语言。

### IC SDK
IC SDK包含以下组件：
- dfx：命令行界面 (CLI)
- moc：Motoko运行时编译器
- replica：IC本地网络二进制
- uninstall.sh：用于删除 SDK 及其所有组件的脚本
- versions：缓存目录，其中包含安装的每个 SDK 版本的子目录
#### SDK的安装：
在Mac/Linux的终端下执行命令
```shell
sh -ci "$(curl -fsSL https://internetcomputer.org/install.sh)"
```
如果是Windows系统，则需要在[WSL](https://learn.microsoft.com/en-us/windows/wsl/install)下执行命令
#### dfx的使用：
DFINITY 命令行执行环境 (`dfx`) 是用于创建、部署和管理IC上 dapp 的主要工具。

你可以使用`dfx`加上相应的的父命令和子命令来执行不同类型的操作。

运行命令的基本语法dfx是：
```shell
dfx [subcommand] [flag]
```
常用命令：

1. 查看某个命令的帮助

    ```shell
    dfx help [subcommand]
    ```
    例如，查看`dfx new` 命令的帮助信息
    ```shell
    dfx help new
    ```
2. 创建新项目
    ```shell
    dfx new _project_name_ [flag]
    ```
    例如，创建一个名为`hello_ic`的新项目
    ```shell
    dfx new hello_ic
    ```
3. 启动本地IC网络环境
    ```shell
    dfx start [option] [flag]
    ```
    例如，在后台启动本地IC网络环境
    ```shell
    dfx start --background
    ```
4. 停止本地IC网络环境
    ```shell
    dfx stop [flag]
    ```
    例如，停止在后台运行的本地IC网络环境
    ```shell
    dfx stop
    ```
5. 根据`dfx.json`，将程序编译为`WASM`
    ```shell
    dfx build [flag] [option] [--all | canister_name]
    ```
    例如，测试容器是能在本地IC环境下进行编译
    ```shell
    dfx build --check
    ```
6. 部署容器和代码到相应网络环境
    ```shell
    dfx deploy [flag] [option] [canister_name]
    ```
    例如，将当前`dfx.json`中的所有canister部署到本地网络环境
    ```shell
    dfx deploy
    ```
    注意，在本地部署前需要先用`dfx start`相关命令启动本地网络环境
    [更多详细用法可参考官方文档](https://internetcomputer.org/docs/current/references/cli-reference/) 
# 实战剖析
我们用一个Web3版本的LinkedIn(领英) demo入手，剖析代码并尝试增加新功能。

项目的[原型demo](https://github.com/dfinity/linkedup)位于Dfinity官方仓库,我们先试着理解代码(这里我们暂时忽略前端部分)。

我们已经知道了我们需要构建的是一个职业信息社交网络，那么主要的功能需求点就是用户能够创建自己的主页，然后对个人主页的相关信息进行修改；用户与用户之间可以关注建立联系等。

我们先看项目根目录下的`dfx.json`文件，可以知道这个项目包含三个`Canister` : `connectd`、`linkedup` 和 `linkedup_assets`，其中`linkedup_assets`是前端资源的`Canister`,我们暂时不关心它；其余两个是后端的`Canister`
```json
{
  "canisters": {
    "connectd": {
      "main": "src/connectd/main.mo"
    },
    "linkedup": {
      "main": "src/linkedup/main.mo"
    },
    "linkedup_assets": {
      "dependencies": ["linkedup"],
      "frontend": {
        "entrypoint": "src/linkedup/public/main.js"
      },
      "type": "assets",
      "source": [
        "src/linkedup/public",
        "dist/linkedup_assets"
      ]
    }
  },
  "defaults": {
    "build": {
      "packtool": ""
    }
  },
  "networks": {
    "tungsten": {
      "providers": ["https://gw.dfinity.network"],
      "type": "persistent"
    },
    "local": {
      "bind": "0.0.0.0:8000",
      "type": "ephemeral"
    }
  },
  "dfx": "0.6.22"
}
```
根据项目信息我们可以知道`linkedup`罐子主要负责业务逻辑，例如创建个人主页、修改个人主页资料等、他也提供了调用`connectd`罐子的api接口，这样的话前端只需要和`linkedup`交互即可。
`connectd`罐子主要负责建立用户间的关系,提供给`linkedup`罐子调用。


我们先仔细剖析下`connected`罐子的代码:
```json
├── connectd
│   ├── digraph.mo
│   ├── main.mo
│   └── types.mo
```
`connected`罐子下面有三个`motoko`文件：

`main.mo`是`connected`罐子的主要代码文件，在这个文件中定义了`actor` 

`types.mo`是`connected`罐子的自定义类型模块文件，被引入`main.mo`中使用

`digraph.mo`是`connected`罐子的模块文件，他定义了一个公共类，`main.mo`可以实例化这个类，使用其api，主要用来处理关系的数据

了解了框架后我们来深入代码一行行的剖析

`types.mo`定义了一些自定义类型
```js
import Principal "mo:base/Principal"; // 引入Principal库, mo:base是motoko的基本库

// 定义一个模块
module { 
  public type Vertex = Principal; // 定义一个公共的"顶点"类型，其本质是Principal类型
};

```

`digraph.mo`也是一个模块文件，其模块中定义了一个“用户关系图”公共类，类中定义了实现用户间关系必需的数据结构和相关方法
```js
import Array "mo:base/Array"; // 引入Array库
import Iter "mo:base/Iter"; // 引入Iter库
import Types "./types"; // 引入自定义类型模块

// 定义模块
module {
  type Vertex = Types.Vertex; // 定义顶点类型为自定义类型中的顶点类型

  // 定义“用户关系图”公共类
  // 在这里我们将“用户”比作一个图的一个”点“，将用户间的”关系“比作图的一条”边“
  public class Digraph() { 

    var vertexList: [Vertex] = []; // 定义“用户点”数组
    var edgeList: [(Vertex, Vertex)] = []; // 定义”用户关系“数组，数组元素的类型是一个元组(from, to)

    // 定义一个公共方法，可以新增一个点到“用户点”数组
    public func addVertex(vertex: Vertex) { // 函数参数名称为vertex，类型为Vertex；没有返回值
      // 在motoko中，var为可变值，可变值的再赋值需要用 ":="
      vertexList := Array.append<Vertex>(vertexList, [vertex]);
    };

    // 定义新增一条边的公共方法
    public func addEdge(fromVertex: Vertex, toVertex: Vertex) {
      edgeList := Array.append<(Vertex, Vertex)>(edgeList, [(fromVertex, toVertex)]);
    };

    // 查询某个顶点的领边
    public func getAdjacent(vertex: Vertex): [Vertex] { 
      var adjacencyList: [Vertex] = [];
      for ((fromVertex, toVertex) in Iter.fromArray<(Vertex, Vertex)>(edgeList)) {
        if (fromVertex == vertex) {
          adjacencyList := Array.append<Vertex>(adjacencyList, [toVertex]);
        };
      };
      adjacencyList
    };

  };
};
```

`main.mo`主要定义了可供外部调用的API接口，图的相关操作是实例化图公共类后调用相关方法
```js
import Digraph "./digraph"; // 引入用户图模块
import Types "./types"; // 引入自定义类型模块

// 定义名称为Connectd的actor
actor Connectd {
  type Vertex = Types.Vertex; // 定义“点”类型

  var graph: Digraph.Digraph = Digraph.Digraph(); // 实例化用户图模块的图公共类
  
  public func healthcheck(): async Bool { true }; // 检查罐子是否正常运行

  // 用户A建立与用户B的关系，供外部调用的方法是async异步的
  public func connect(userA: Vertex, userB: Vertex): async () {
    graph.addEdge(userA, userB);
  };

  // 获取一个用户的关系（用户点的邻边）
  public func getConnections(user: Vertex): async [Vertex] {
    graph.getAdjacent(user)
  };

};
```
了解完`connected`罐子后我们再来看`linkedup`罐子，我们需要记住，`connected`罐子是为`linkedup`罐子“服务”的。

同样的，我们看一下`linkedup`罐子代码的目录结构：
```json
linkedup
    ├── database.mo
    ├── main.mo
    ├── types.mo
    └── utils.mo
```
`linkedup`罐子同样有`main.mo`和`type.mo`，`main.mo`是负责主要业务逻辑的, `types.mo`负责新类型的定义

而新增的`utils.mo`模块主要有常用的实用方法

`database.mo`则与上文的`digraph.mo`类似，其定义了一个简易的数据库模块，主要用于处理业务逻辑所涉及的数据结构和相应处理方法

我们接着深入代码

`types.mo`定义了`UserId、NewProfile、Profile`三个类型
```js
import Principal "mo:base/Principal";

module {
  public type UserId = Principal;

  public type NewProfile = {
    firstName: Text; // Text为文本类型
    lastName: Text;
    title: Text;
    company: Text;
    experience: Text;
    education: Text;
    imgUrl: Text;
  };

  public type Profile = {
    id: UserId;
    firstName: Text;
    lastName: Text;
    title: Text;
    company: Text;
    experience: Text;
    education: Text;
    imgUrl: Text;
  };
};
```
`database.mo`模块定义了名为目录的公共类，公共类中定义了一个Key为`UserId`、value为`Profile`的`hashmap`，用于存储用户的主页信息
```js
import Array "mo:base/Array";
import HashMap "mo:base/HashMap";
import Iter "mo:base/Iter";
import Option "mo:base/Option";
import Principal "mo:base/Principal";
import Types "./types";

module {
  type NewProfile = Types.NewProfile;
  type Profile = Types.Profile;
  type UserId = Types.UserId;

  // 定义“目录”公共类
  public class Directory() {
    // The "database" is just a local hash map
    let hashMap = HashMap.HashMap<UserId, Profile>(1, isEq, Principal.hash);

    // 新增一个profile
    public func createOne(userId: UserId, profile: NewProfile) {
      hashMap.put(userId, makeProfile(userId, profile));
    };

    // 修改某个用户的新增一个profile
    public func updateOne(userId: UserId, profile: Profile) {
      hashMap.put(userId, profile);
    };
     
    // 查询某个用户的Profile
    // 返回值为?Profile，表示返回值是Option的，如果没有查询到结果则返回null
    public func findOne(userId: UserId): ?Profile {
      hashMap.get(userId)
    };

    // 查询一组用户的Profile
    public func findMany(userIds: [UserId]): [Profile] {
      func getProfile(userId: UserId): Profile {
        Option.unwrap<Profile>(hashMap.get(userId))
      };
      Array.map<UserId, Profile>(userIds, getProfile)
    };

    // 根据名字查询用户Profile
    public func findBy(term: Text): [Profile] {
      var profiles: [Profile] = [];
      for ((id, profile) in hashMap.entries()) {
        let fullName = profile.firstName # " " # profile.lastName; // 字符串拼接
        if (includesText(fullName, term)) {
          profiles := Array.append<Profile>(profiles, [profile]);
        };
      };
      profiles
    };

    // Helpers
    // 用UserId和NewProfile生成Profile类型实例
    func makeProfile(userId: UserId, profile: NewProfile): Profile {
      {
        id = userId;
        firstName = profile.firstName;
        lastName = profile.lastName;
        title = profile.title;
        company = profile.company;
        experience = profile.experience;
        education = profile.education;
        imgUrl = profile.imgUrl;
      }
    };

    func includesText(string: Text, term: Text): Bool {
      let stringArray = Iter.toArray<Char>(string.chars());
      let termArray = Iter.toArray<Char>(term.chars());

      var i = 0;
      var j = 0;

      while (i < stringArray.size() and j < termArray.size()) {
        if (stringArray[i] == termArray[j]) {
          i += 1;
          j += 1;
          if (j == termArray.size()) { return true; }
        } else {
          i += 1;
          j := 0;
        }
      };
      false
    };
  };

  func isEq(x: UserId, y: UserId): Bool { x == y };
};
```
`utils.mo`则定义了一些实用方法
```js
import Array "mo:base/Array";
import Option "mo:base/Option";
import Database "./database";
import Types "./types";

module {
  type NewProfile = Types.NewProfile;
  type Profile = Types.Profile;
  type UserId = Types.UserId;

  // Profiles
  // 提供数据库模块的“目录”类实例和userId来查询Profile
  public func getProfile(directory: Database.Directory, userId: UserId): Profile {
    let existing = directory.findOne(userId);
    switch (existing) {
      case (?existing) { existing };
      case (null) {
        {
          id = userId;
          firstName = "";
          lastName = "";
          title = "";
          company = "";
          experience = "";
          education = "";
          imgUrl = "";
        }
      };
    };
  };

  // Connections
  // x是否包含在xs数组中
  public func includes(x: UserId, xs: [UserId]): Bool {
    func isX(y: UserId): Bool { x == y };
    switch (Array.find<UserId>(xs, isX)) {
      case (null) { false };
      case (_) { true };
    };
  };

  // Authorization 权限管理

  let adminIds: [UserId] = [];

  public func isAdmin(userId: UserId): Bool {
    func identity(x: UserId): Bool { x == userId };
    Option.isSome(Array.find<UserId>(adminIds,identity))
  };

  public func hasAccess(userId: UserId, profile: Profile): Bool {
    userId == profile.id or isAdmin(userId)
  };
};
```
`main.mo`则定义了`actor`，负责业务逻辑
```js
// Make the Connectd app's public methods available locally
import Connectd "canister:connectd";
import Database "./database";
import Types "./types";
import Utils "./utils";

actor LinkedUp {
  var directory: Database.Directory = Database.Directory(); // 实例化数据库模块中的目录类

  type NewProfile = Types.NewProfile;
  type Profile = Types.Profile;
  type UserId = Types.UserId;

  // Healthcheck

  public func healthcheck(): async Bool { true };

  // Profiles

  // 用户创建主页
  public shared(msg) func create(profile: NewProfile): async () {
    directory.createOne(msg.caller, profile); // 调用directory类的方法
  };

  // 用户修改信息
  public shared(msg) func update(profile: Profile): async () {
    if(Utils.hasAccess(msg.caller, profile)) { // 检查用户是否有权限
      directory.updateOne(profile.id, profile);
    };
  };
  
  // 查询用户的主页信息
  public query func get(userId: UserId): async Profile {
    Utils.getProfile(directory, userId)
  };

  // 用名字查询用户信息
  public query func search(term: Text): async [Profile] {
    directory.findBy(term)
  };

  // Connections

  // 调用者与另一个用户连接
  public shared(msg) func connect(userId: UserId): async () {
    // Call Connectd's public methods without an API
    // 调用Connectd罐子的公共方法API, await等待结果
    await Connectd.connect(msg.caller, userId);
  };

  // 查询某人的好友的Profile
  public func getConnections(userId: UserId): async [Profile] {
    let userIds = await Connectd.getConnections(userId);
    directory.findMany(userIds)
  };

  // 查询某人是否是调用者的好友
  public shared(msg) func isConnected(userId: UserId): async Bool {
    let userIds = await Connectd.getConnections(msg.caller);
    Utils.includes(userId, userIds)
  };

  // User Auth
  // 查询调用者的Principal Id
  public shared query(msg) func getOwnId(): async UserId { msg.caller }

};
```

了解完代码架构和细节后，我们尝试[编译部署该项目](https://github.com/xiaoyuanxun/linkedup)。

我们进入项目根目录，

然后先启动IC本地网络
```shell
dfx start
```
然后在终端可以看到类似如下信息
```shell
Running dfx start for version 0.14.3

Initialized replica.

Dashboard: http://localhost:55777/_/dashboard
```
这表示IC本地网络成功启动，我们可以打开上面的Dashboard链接，查看网络状态等

![网络状态仪表盘](./assets/image-1-1.png)

然后我们编译部署Canister
```shell
dfx deploy
```
终端会显示如下信息
```shell
Deploying all canisters.
Creating a wallet canister on the local network.
The wallet canister on the "local" network for user "default" is "bnz7o-iuaaa-aaaaa-qaaaa-cai"
Creating canisters...
Creating canister connectd...
connectd canister created with canister id: bkyz2-fmaaa-aaaaa-qaaaq-cai
Creating canister linkedup...
linkedup canister created with canister id: bd3sg-teaaa-aaaaa-qaaba-cai
Creating canister linkedup_assets...
linkedup_assets canister created with canister id: be2us-64aaa-aaaaa-qaabq-cai
Building canisters...

Building frontend...
Installing canisters...
Creating UI canister on the local network.
The UI canister on the "local" network is "br5f7-7uaaa-aaaaa-qaaca-cai"
Installing code for canister connectd, with canister ID bkyz2-fmaaa-aaaaa-qaaaq-cai
Installing code for canister linkedup, with canister ID bd3sg-teaaa-aaaaa-qaaba-cai
Installing code for canister linkedup_assets, with canister ID be2us-64aaa-aaaaa-qaabq-cai
Uploading assets to asset canister...
Fetching properties for all assets in the canister.
Starting batch.
Staging contents of new and changed assets in batch 1:
  /index.html 1/1 (7196 bytes) sha c7545fb06d97f8eede0a28524035f8c909e2ad456e26da2e29e4510bd87b8eb4 
  /templates.js 1/1 (3188 bytes) sha 9a29bc111afcbbf07194ee385d7c7c5dc084fc6a5c545f9f3a75e01aba19d4bd 
  /templates.js (gzip) 1/1 (772 bytes) sha 2b9db874caa4da5e3fa029c49c9f1970846d8f184dbbed38ada826b340525239 
  /index.js.LICENSE.txt 1/1 (494 bytes) sha bce8afa69662344f3076e34d586e8a319541e6220f7d33d24c638d2e41e8b3f5 
  /index.js.LICENSE.txt (gzip) 1/1 (303 bytes) sha 825daec9b568cb21931839f253a52c6568c67067f728b017d07e3e8e4ab9ad4b 
  /index.js.map (gzip) 1/1 (486875 bytes) sha 292293e601f5d89b74f738059ac3de6f0f8880de21e4b02900f5b3dde4b7f133 
  /index.js.map 2/2 (512088 bytes) sha d3bc05cedd9145e6e95ac69e617677e9362afab04be5c2aaec484e5ea18b6e91 
  /index.js 1/1 (1532332 bytes) sha 9a45f461b9eb3685fd69f74e51717a809a2781621308f32c78c36c44f9ac7b09 
  /utils.js 1/1 (267 bytes) sha d931b0c93683a02c41427208d424ad52239ae1f5ada1d8dbea5a3fd041969c13 
  /index.html (gzip) 1/1 (1448 bytes) sha 532c56fb849e459afdeee85fa91f59ac9990ad03b8db655bc3aa36acff1e65c6 
  /utils.js (gzip) 1/1 (199 bytes) sha 054727654de247397e5db169d1399ad9001a9a19cc8381e2a3a7d184ee87d32a 
  /index.js.map 1/2 (1900000 bytes) sha d3bc05cedd9145e6e95ac69e617677e9362afab04be5c2aaec484e5ea18b6e91 
  /index.js (gzip) 1/1 (256156 bytes) sha cf3ba66fe82622bb31d124a1c8daa3abbf387760a858e3a25f6a27b161230fbb 
  /main.js (gzip) 1/1 (2038 bytes) sha 1ebe40bb131aed3f67823a56b0536387c6a4820855f4e3db3f9879ff7ca9289f 
  /main.js 1/1 (7682 bytes) sha bea749dd6d5fe8dace9812606f02eb76754c31992ecf2e69801de5585cf3f87c 
  /index.css 1/1 (15386 bytes) sha 5488d6d439f6abdc3104b499e399ce2f588448ce81be46cbdb9a5e9ef2bedf2b 
  /index.css (gzip) 1/1 (3082 bytes) sha 166ed706f6011b8b95c3160cca68d47955421dd29d64d609e6e6d4cf6b3c47a6 
Committing batch.
Committing batch with 24 operations.
Deployed canisters.
URLs:
  Frontend canister via browser
    linkedup_assets: http://127.0.0.1:4943/?canisterId=be2us-64aaa-aaaaa-qaabq-cai
  Backend canister via Candid interface:
    connectd: http://127.0.0.1:4943/?canisterId=br5f7-7uaaa-aaaaa-qaaca-cai&id=bkyz2-fmaaa-aaaaa-qaaaq-cai
    linkedup: http://127.0.0.1:4943/?canisterId=br5f7-7uaaa-aaaaa-qaaca-cai&id=bd3sg-teaaa-aaaaa-qaaba-cai
```
我们一段段来理解`dfx deploy`做了哪些工作

首先，在本地网络给`default`用户创建了一个钱包罐子，其钱包默认有一些Cycles，将用来创建和管理其他Canister
```shell
Creating a wallet canister on the local network.
The wallet canister on the "local" network for user "default" is "bnz7o-iuaaa-aaaaa-qaaaa-cai"
```

然后创建了业务需要的2个后端`Canister`和一个前端`Asset Canister`
```shell
Creating canisters...
Creating canister connectd...
connectd canister created with canister id: bkyz2-fmaaa-aaaaa-qaaaq-cai
Creating canister linkedup...
linkedup canister created with canister id: bd3sg-teaaa-aaaaa-qaaba-cai
Creating canister linkedup_assets...
linkedup_assets canister created with canister id: be2us-64aaa-aaaaa-qaabq-cai
```

然后编译后端和前端代码为可部署在`Canister`中的`Wasm`文件
```shell
Building canisters...
Building frontend...
```
然后将`Wasm`文件安装到`Canister`中
```shell
Installing canisters...
Creating UI canister on the local network.
The UI canister on the "local" network is "br5f7-7uaaa-aaaaa-qaaca-cai"
Installing code for canister connectd, with canister ID bkyz2-fmaaa-aaaaa-qaaaq-cai
Installing code for canister linkedup, with canister ID bd3sg-teaaa-aaaaa-qaaba-cai
Installing code for canister linkedup_assets, with canister ID be2us-64aaa-aaaaa-qaabq-cai
Deployed canisters.
```

上传前端静态资源到`Asset Canister`中
```shell
Uploading assets to asset canister...
Fetching properties for all assets in the canister.
Starting batch.
Staging contents of new and changed assets in batch 1:
  /index.html 1/1 (7196 bytes) sha c7545fb06d97f8eede0a28524035f8c909e2ad456e26da2e29e4510bd87b8eb4 
  /templates.js 1/1 (3188 bytes) sha 9a29bc111afcbbf07194ee385d7c7c5dc084fc6a5c545f9f3a75e01aba19d4bd 
  /templates.js (gzip) 1/1 (772 bytes) sha 2b9db874caa4da5e3fa029c49c9f1970846d8f184dbbed38ada826b340525239 
  /index.js.LICENSE.txt 1/1 (494 bytes) sha bce8afa69662344f3076e34d586e8a319541e6220f7d33d24c638d2e41e8b3f5 
  /index.js.LICENSE.txt (gzip) 1/1 (303 bytes) sha 825daec9b568cb21931839f253a52c6568c67067f728b017d07e3e8e4ab9ad4b 
  /index.js.map (gzip) 1/1 (486875 bytes) sha 292293e601f5d89b74f738059ac3de6f0f8880de21e4b02900f5b3dde4b7f133 
  /index.js.map 2/2 (512088 bytes) sha d3bc05cedd9145e6e95ac69e617677e9362afab04be5c2aaec484e5ea18b6e91 
  /index.js 1/1 (1532332 bytes) sha 9a45f461b9eb3685fd69f74e51717a809a2781621308f32c78c36c44f9ac7b09 
  /utils.js 1/1 (267 bytes) sha d931b0c93683a02c41427208d424ad52239ae1f5ada1d8dbea5a3fd041969c13 
  /index.html (gzip) 1/1 (1448 bytes) sha 532c56fb849e459afdeee85fa91f59ac9990ad03b8db655bc3aa36acff1e65c6 
  /utils.js (gzip) 1/1 (199 bytes) sha 054727654de247397e5db169d1399ad9001a9a19cc8381e2a3a7d184ee87d32a 
  /index.js.map 1/2 (1900000 bytes) sha d3bc05cedd9145e6e95ac69e617677e9362afab04be5c2aaec484e5ea18b6e91 
  /index.js (gzip) 1/1 (256156 bytes) sha cf3ba66fe82622bb31d124a1c8daa3abbf387760a858e3a25f6a27b161230fbb 
  /main.js (gzip) 1/1 (2038 bytes) sha 1ebe40bb131aed3f67823a56b0536387c6a4820855f4e3db3f9879ff7ca9289f 
  /main.js 1/1 (7682 bytes) sha bea749dd6d5fe8dace9812606f02eb76754c31992ecf2e69801de5585cf3f87c 
  /index.css 1/1 (15386 bytes) sha 5488d6d439f6abdc3104b499e399ce2f588448ce81be46cbdb9a5e9ef2bedf2b 
  /index.css (gzip) 1/1 (3082 bytes) sha 166ed706f6011b8b95c3160cca68d47955421dd29d64d609e6e6d4cf6b3c47a6 
Committing batch.
Committing batch with 24 operations.
```

还在本地创建了`UI Canister`，他可以用来形象化调用和测试后端`Canister`接口
```shell
Creating UI canister on the local network.
The UI canister on the "local" network is "br5f7-7uaaa-aaaaa-qaaca-cai"
```
最后显示部署成功的链接，
我们可以点击`linkedup_assets`的链接来查看前端

点击`connectd`和`linkedup`可以通过`candidUI`来调试后端接口
```shell
Deployed canisters.
URLs:
  Frontend canister via browser
    linkedup_assets: http://127.0.0.1:4943/?canisterId=be2us-64aaa-aaaaa-qaabq-cai
  Backend canister via Candid interface:
    connectd: http://127.0.0.1:4943/?canisterId=br5f7-7uaaa-aaaaa-qaaca-cai&id=bkyz2-fmaaa-aaaaa-qaaaq-cai
    linkedup: http://127.0.0.1:4943/?canisterId=br5f7-7uaaa-aaaaa-qaaca-cai&id=bd3sg-teaaa-aaaaa-qaaba-cai
```

我们打开`linkedup candidUI`窗口来测试一下后端的业务逻辑

![candidUI界面](./assets/image-1-2.png)

我们尝试调用`create`接口来创建个人主页信息

输入信息后点击`call`

可以看到成功返回`()`，用时2.107s，因为`update`方法(也就是定义为`public shared`的方法)需要进行共识，一般会花费2s左右

![调用create方法](./assets/image-1-3.png)

我们也可以通过接口`get`来查询某人的主页信息

其返回的是一个`record`结构，而这次调用仅用了0.015s就返回了结果，因为`query`方法不经过共识，基本是ms级别的

![调用get方法](./assets/image-1-4.png)

其他方法的调用大家可以自己尝试

candidUI其实也不是万能的，其最主要的缺点是身份单一，比如我们就无法测试`connect`业务逻辑，让两个身份建立联系

所以上线业务一般用`Rust Agent`或`JS Agent`库来编写自动化测试

至此，我们了解完了这个demo，可以停止本地IC网络
```shell
dfx stop
```







## 1 小时入门 Rust：屏蔽 Rust 复杂语法，直接上手在开发中学习

> 在Rust中，结构体是干什么用的

在 Rust 中，结构体（structure）是用来定义自定义数据类型的一种方法。结构体可以包含多个不同类型的数据字段，并且每个字段都可以被访问和操作。结构体在 Rust 中用于创建复杂的数据类型，例如，它们可以表示面向对象编程中的类，并且也可以实现多重继承。



> 在Rust中，Vec是干什么用的？

`Vec` 是 Rust 中的动态数组类型，它允许在运行时增加或删除元素。它的大小可以随着需求的增加而自动增加，而且也可以随着元素的删除而自动减小。它提供了许多方便的方法，如 push，pop，iterate 等，方便处理集合数据类型。



> 那结构体就相当于 Java 的 ‘ 类 ’ 了吧

是的，结构体在 Rust 中的作用与 Java 的类类似，都可以用来描述对象，封装数据和方法。不过 Rust 的结构体与 Java 的类的语法和实现有所不同，比如 Rust 中的结构体可以选择实现关联函数，而 Java 的类则不能。



> 关联函数是什么东西

在 Rust 中，关联函数是使用 `impl` 关键字声明的静态函数，它不依赖于任何具体实例。它们通常用于创建一个新的实例或返回一个常量，例如：

```rust
struct Circle {
    radius: f64,
}

impl Circle {
    fn new(radius: f64) -> Self {
        Circle { radius }
    }

    fn area(&self) -> f64 {
        std::f64::consts::PI * self.radius * self.radius
    }
}
```

在上面的代码片段中，`new` 函数是一个关联函数，它创建一个新的圆形实例，而 `area` 函数是一个实例函数，它接受一个圆形实例作为参数。



> 请具体解释：实例

在 Rust 中，一个实例就是结构体或者枚举的一个具体的对象，可以通过它来调用方法，访问字段等。

例如：

```rust
struct Rectangle {
    width: u32,
    height: u32,
}

impl Rectangle {
    fn area(&self) -> u32 {
        self.width * self.height
    }
}

fn main() {
    let rect = Rectangle { width: 30, height: 50 };
    println!("The area of the rectangle is {} square pixels.", rect.area());
}
```

这里我们定义了一个名为`Rectangle`的结构体，其中有两个字段 `width` 和 `height`。然后我们实现了一个名为 `area` 的方法，该方法可以返回矩形的面积。最后我们在 `main` 函数中创建了一个矩形实例，并使用它调用了 `area` 方法。



> Rust里的枚举是什么意思

Rust 的枚举是一种可以将多个具有相关性的命名值组合在一起的数据类型。枚举中的每个命名值称为枚举项，可以是一个整数值或其他类型的值。枚举在 Rust 中是一种很强大的数据类型，可以让你在代码中更容易表达概念和意图。

举个例子，你可以定义一个枚举来表示星期：

```rust
enum Weekday {
    Monday,
    Tuesday,
    Wednesday,
    Thursday,
    Friday,
    Saturday,
    Sunday,
}
```

这个枚举有7个枚举项，每个枚举项都代表了一个星期中的一天。你可以使用枚举项来表示一个星期中的某一天：

```rust
let today = Weekday::Monday;
```



### 资料参考

https://icpleague.com/thread/145

https://wiki.internetcomputer.org/wiki/Index_of_dapps_on_the_Internet_Computer_ecosystem

https://space.bilibili.com/627399227/favlist?fid=1590361227&ftype=create

https://www.aliyundrive.com/drive/folder/62836b6a752ab8ef1a4e4a20ae1603521a7d7485

https://www.aliyundrive.com/drive/folder/62a0035d7a2f0f94e24e457d9544c00f37376ffd

https://github.com/motoko-bootcamp/motokobootcamp-2023

https://github.com/motoko-bootcamp/bootcamp-2022

https://github.com/Dfinity-Bjoern/Magnify

https://kyle-peacock.com/blog

https://kyle-peacock.com/blog/dfinity/integrating-internet-identity

https://juejin.cn/post/7174064801516109855

https://juejin.cn/post/7172945742783315998

https://juejin.cn/post/7176617858070708282

https://juejin.cn/post/7177032973777600549

https://juejin.cn/post/7172791050879107086

https://juejin.cn/post/7184357581186891832

https://juejin.cn/post/7177032973777600549

https://github.com/Web3NL/motoko-book

https://www.bilibili.com/video/BV1DA4y1o77N/?vd_source=db5bf130bc81934e0880088323df6748



https://github.com/dfinity/awesome-icp

https://github.com/motoko-unofficial/awesome-motoko


安装 dfx 后，会在本地生成一对公私钥，对应开发者的 principal id 。



pid 是开发者身份，在本地部署 dapp 和在主网上部署 dapp 用的都是同一个 pid 。

只是本地部署用本地钱包；在主网部署需要在主网上安装一个钱包，通过这个钱包来部署 canister 。其实这个钱包也是一个 canister ，开发者部署 dapp 时，是先检查开发者身份在主网上钱包里 cycles 的余额，然后通过钱包 canister 在主网上创建一个新 canister ，在新 canister 里安装编译好的 WASM 字节码。

（上段文字的示意图）